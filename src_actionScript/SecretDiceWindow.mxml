<?xml version="1.0" encoding="utf-8"?>
<torgtaitai:CommonPopupWindow
      xmlns:mx="http://www.adobe.com/2006/mxml"
      xmlns:torgtaitai="*" 
      width="450" height="110"
      title="{Language.s.secretDiceResult}" 
      showCloseButton="true"
      close="closeWindow()"
      >
  
  <mx:Box id="secretDiceResultSpace" height="100%" width="100%">
    
  </mx:Box>
  
  <mx:Script>
    <![CDATA[
    import mx.managers.PopUpManager;
    import mx.controls.TextArea;
    import mx.controls.Button;
    import mx.containers.HBox;
    import mx.core.UIComponent;
    
    static private var self:SecretDiceWindow = null;
    
    static public function printMessage(data:ChatSendData):String {
        if( self == null ) {
            self = DodontoF.popup(SecretDiceWindow, false) as SecretDiceWindow;
        }
        
        return self.printMessage(data);
    }
    
    static private var windowHeigthLimit:int = 450;
    static private var boxHeigth:int = 50;
    
    public function printMessage(chatSendData:ChatSendData):String {
        var message:String = chatSendData.getMessage();
        
        var box:HBox = new HBox();
        box.width  = this.width - 50;
        box.height  = boxHeigth;
        
        var textArea:TextArea = new TextArea()
        textArea.width = 265;
        textArea.height = boxHeigth;
        textArea.editable = false;
        textArea.text = message;
        box.addChild(textArea);
        
        var openButton:Button = getOpenSecretDiceResultButton(textArea, chatSendData, box);
        box.addChild(openButton);
        
        var deleteButton:Button = getDeleteSecretDiceResultButton(secretDiceResultSpace, box);
        box.addChild(deleteButton);
        
        secretDiceResultSpace.addChild(box);
        
        setHeight();
        
        return message;
    }
    
    private function setHeight():void {
        secretDiceResultSpace.validateNow();
        
        var newHeight:int = secretDiceResultSpace.numChildren * (boxHeigth + 6) + 50;
        if( newHeight > windowHeigthLimit ) {
            newHeight = windowHeigthLimit;
        }
        
        this.height = newHeight;
    }
    
    private function getOpenSecretDiceResultButton(textArea:TextArea, chatSendData:ChatSendData, box:Box):Button {
        var button:Button = new Button();
        
        button.label = Language.s.openSecretDiceResult;
        button.height = boxHeigth;
        button.addEventListener(MouseEvent.CLICK, getClickSendButtonEvent(textArea, chatSendData, box));
        
        return button;
    }
    
    private function getClickSendButtonEvent(textArea:TextArea, chatSendData:ChatSendData, box:Box):Function {
        return function(event:MouseEvent = null):void {
            
            var data:ChatSendData = new ChatSendData(chatSendData.getChannel(), chatSendData.getMessage());
            data.setDiceRollResult();
            DodontoF_Main.getInstance().getChatWindow().sendDiceRollResultMessage(data);
            
            secretDiceResultSpace.removeChild(box);
            setHeight();
        };
    }
    
    private function getDeleteSecretDiceResultButton(parent:UIComponent, child:UIComponent):Button {
        var button:Button = new Button();
        
        button.label = Language.s.deleteSecretDiceResult;
        button.height = boxHeigth;
        button.addEventListener(MouseEvent.CLICK, function(event:MouseEvent = null):void {
                parent.removeChild(child);
            });
        
        return button;
    }
    
    
    public function closeWindow():void {
        self = null;
        PopUpManager.removePopUp(this);
    }
    
    ]]>
  </mx:Script>
  
</torgtaitai:CommonPopupWindow>
